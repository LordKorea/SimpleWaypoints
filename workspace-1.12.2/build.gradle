buildscript {
    repositories {
        jcenter()
        maven { url = "https://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'
    }
}

plugins {
    id "io.franzbecker.gradle-lombok" version '1.6'
    id 'com.github.johnrengelman.shadow' version '1.2.4'
}

apply plugin: 'net.minecraftforge.gradle.forge'
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.


version = "1.0.0-1.12.2.2838"
group = "io.gitlab.lordkorea.simplewaypoints"
archivesBaseName = "simplewaypoints"

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.5.2838"
    runDir = "run"
    mappings = "snapshot_20171003"

    replace "@VERSION@", project.version
    replace "@FINGERPRINT@", project.signSHA1
    replaceIn "SimpleWaypointsMod.java"
}

repositories {
    mavenLocal()
}

dependencies {
    compile 'nge.lk.mods.commonlib:commonlib:1.0.0-1.12.2.2838'
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

reobf {
    shadowJar { mappingType = 'SEARGE' }
}

tasks.build.dependsOn reobfShadowJar

shadowJar {
    classifier = null
    dependencies {
        include(dependency('nge.lk.mods.commonlib:commonlib:1.0.0-1.12.2.2838'))
    }
    exclude 'dummyThing'
    relocate 'nge.lk.mods.commonlib', 'nge.lk.mods.simpletabs.common'
}

task signJar(type: SignJar, dependsOn: reobfJar) {
    keyStore = project.keyStore
    alias = project.keyStoreAlias
    storePass = project.keyStorePass
    keyPass = project.keyStoreKeyPass
    inputFile = jar.archivePath
    outputFile = jar.archivePath
}

build.dependsOn signJar
